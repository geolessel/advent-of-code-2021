# Day 4 - Giant Squid

## Part 1

You're already almost 1.5km (almost a mile) below the surface of the ocean, already so deep that you can't see any sunlight. What you can see, however, is a giant squid that has attached itself to the outside of your submarine.

Maybe it wants to play bingo?

Bingo is played on a set of boards each consisting of a 5x5 grid of numbers. Numbers are chosen at random, and the chosen number is marked on all boards on which it appears. (Numbers may not appear on all boards.) If all numbers in any row or any column of a board are marked, that board wins. (Diagonals don't count.)

The submarine has a bingo subsystem to help passengers (currently, you and the giant squid) pass the time. It automatically generates a random order in which to draw numbers and a random set of boards (your puzzle input). For example:

`7,4,9,5,11,17,23,2,0,14,21,24,10,16,13,6,15,25,12,22,18,20,8,19,3,26,1`

```
22 13 17 11  0
 8  2 23  4 24
21  9 14 16  7
 6 10  3 18  5
 1 12 20 15 19

 3 15  0  2 22
 9 18 13 17  5
19  8  7 25 23
20 11 10 24  4
14 21 16 12  6

14 21 17 24  4
10 16 15  9 19
18  8 23 26 20
22 11 13  6  5
 2  0 12  3  7
```

After the first five numbers are drawn (`7`, `4`, `9`, `5`, and `11`), there are no winners, but the boards are marked as follows (shown here adjacent to each other to save space):

```
22 13 17 11  0         3 15  0  2 22        14 21 17 24  4
 8  2 23  4 24         9 18 13 17  5        10 16 15  9 19
21  9 14 16  7        19  8  7 25 23        18  8 23 26 20
 6 10  3 18  5        20 11 10 24  4        22 11 13  6  5
 1 12 20 15 19        14 21 16 12  6         2  0 12  3  7
```

After the next six numbers are drawn (`17`, `23`, `2`, `0`, `14`, and `21`), there are still no winners:

```
22 13 17 11  0         3 15  0  2 22        14 21 17 24  4
 8  2 23  4 24         9 18 13 17  5        10 16 15  9 19
21  9 14 16  7        19  8  7 25 23        18  8 23 26 20
 6 10  3 18  5        20 11 10 24  4        22 11 13  6  5
 1 12 20 15 19        14 21 16 12  6         2  0 12  3  7
```

Finally, `24` is drawn:

```
22 13 17 11  0         3 15  0  2 22        14 21 17 24  4
 8  2 23  4 24         9 18 13 17  5        10 16 15  9 19
21  9 14 16  7        19  8  7 25 23        18  8 23 26 20
 6 10  3 18  5        20 11 10 24  4        22 11 13  6  5
 1 12 20 15 19        14 21 16 12  6         2  0 12  3  7
```

At this point, the third board wins because it has at least one complete row or column of marked numbers (in this case, the entire top row is marked: 14 21 17 24 4).

The **score** of the winning board can now be calculated. Start by finding the **sum of all unmarked numbers** on that board; in this case, the sum is 188. Then, multiply that sum by **the number that was just called** when the board won, `24`, to get the final score, `188 * 24 = 4512`.

To guarantee victory against the giant squid, figure out which board will win first. **What will your final score be if you choose that board?**

<!-- livebook:{"break_markdown":true} -->

### Part 1 Solution

<!-- livebook:{"livebook_object":"cell_input","name":"Puzzle input","props":{"options":["Test","Real"]},"type":"select","value":"Real"} -->

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Card do
  defstruct rows: [], bingo: false

  defmodule Cell do
    defstruct number: nil, filled: false
  end

  def find_bingo(%Card{rows: rows}) do
    case find_bingo_row(rows) || find_bingo_col(rows) do
      nil -> false
      row -> row
    end
  end

  def mark_number(card, number) do
    card =
      card
      |> Map.update(:rows, [], fn rows ->
        rows
        |> Enum.map(fn row ->
          Enum.map(row, fn
            %Cell{number: ^number} = cell -> Map.put(cell, :filled, true)
            cell -> cell
          end)
        end)
      end)

    case find_bingo(card) do
      false -> card
      _ -> Map.put(card, :bingo, true)
    end
  end

  def find_bingo_row(rows) do
    rows
    |> Enum.find(fn row ->
      Enum.all?(row, fn cell -> cell.filled end)
    end)
  end

  def find_bingo_col(rows) do
    rows
    |> Enum.zip()
    |> Enum.map(&Tuple.to_list/1)
    |> find_bingo_row()
  end
end

defmodule Bingo do
  defstruct cards: [], winner: nil, winners: []

  def play(game, numbers) do
    call_number(game, numbers, 0)
  end

  # defp call_number(%Game{winner: card} when not is_nil(card), _rest), do: {card, called}
  defp call_number(game, [], _i), do: game

  defp call_number(%Bingo{cards: cards} = game, [called | rest], i) do
    cards = Enum.map(cards, fn card -> Card.mark_number(card, called) end)

    case Enum.find(cards, fn card -> card.bingo end) do
      nil ->
        call_number(%{game | cards: cards}, rest, i + 1)

      card ->
        winner = %{i: i, called: called, card: card}

        game
        |> Map.put(:winner, winner)
        |> Map.put(:winners, [winner | game.winners])
        |> Map.put(:cards, Enum.reject(cards, & &1.bingo))
        |> call_number(rest, i + 1)
    end
  end
end
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
{[numbers], cards} =
  case String.trim(IO.gets("Puzzle input")) do
    "Test" ->
      ~s"""
      7,4,9,5,11,17,23,2,0,14,21,24,10,16,13,6,15,25,12,22,18,20,8,19,3,26,1

      22 13 17 11  0
      8  2 23  4 24
      21  9 14 16  7
      6 10  3 18  5
      1 12 20 15 19

      3 15  0  2 22
      9 18 13 17  5
      19  8  7 25 23
      20 11 10 24  4
      14 21 16 12  6

      14 21 17 24  4
      10 16 15  9 19
      18  8 23 26 20
      22 11 13  6  5
      2  0 12  3  7

      """

    "Real" ->
      File.read!("04-input.txt")
  end
  |> String.split("\n", trim: true)
  |> Enum.split(1)

numbers = String.split(numbers, ",", trim: true) |> Enum.map(&String.to_integer/1)

cards =
  cards
  |> Enum.reject(&(String.trim(&1) == ""))
  |> Enum.map(fn line ->
    line
    |> String.split(~r/\s+/, trim: true)
    |> Enum.map(&%Card.Cell{number: String.to_integer(&1)})
  end)
  |> Enum.chunk_every(5)
  |> Enum.map(&%Card{rows: &1})

game = %Bingo{cards: cards}

:ok
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
%{card: card, called: called} =
  game
  |> Bingo.play(numbers)
  |> Map.get(:winners)
  |> Enum.reverse()
  |> hd()

card
|> Map.get(:rows)
|> Enum.flat_map(fn row -> Enum.reject(row, & &1.filled) end)
|> Enum.map(& &1.number)
|> Enum.sum()
|> Kernel.*(called)
```

<!-- livebook:{"branch_parent_index":0} -->

## Part 2

On the other hand, it might be wise to try a different strategy: let the giant squid win.

You aren't sure how many bingo boards a giant squid could play at once, so rather than waste time counting its arms, the safe thing to do is to figure out which board will win last and choose that one. That way, no matter which boards it picks, it will win for sure.

In the above example, the second board is the last to win, which happens after `13` is eventually called and its middle column is completely marked. If you were to keep playing until this point, the second board would have a sum of unmarked numbers equal to `148` for a final score of `148 * 13 = 1924`.

Figure out which board will win last. Once it wins, what would its final score be?

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
%{card: card, called: called} =
  game
  |> Bingo.play(numbers)
  |> Map.get(:winners)
  |> hd()

card
|> Map.get(:rows)
|> Enum.flat_map(fn row -> Enum.reject(row, & &1.filled) end)
|> Enum.map(& &1.number)
|> Enum.sum()
|> Kernel.*(called)
```
